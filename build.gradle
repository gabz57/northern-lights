import org.springframework.boot.gradle.plugin.SpringBootPlugin

import static org.owasp.dependencycheck.reporting.ReportGenerator.Format

//import org.springframework.cloud.contract.verifier.plugin.SpringCloudContractVerifierGradlePlugin

buildscript {
    dependencies {
        classpath "org.owasp:dependency-check-gradle:6.2.2"
    }
}

plugins {
    id 'idea'
    // select the tag to show dependencies versions
    // https://github.com/spring-projects/spring-boot/blob/master/spring-boot-project/spring-boot-dependencies/build.gradle
    id 'org.springframework.boot' version '2.6.4' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.experimental.aot' version '0.11.0' apply false
    id 'org.graalvm.buildtools.native' version '0.9.8' apply false
    id 'com.github.johnrengelman.processes' version '0.5.0' apply false
    id 'org.springdoc.openapi-gradle-plugin' version '1.3.3' apply false
}

idea {
    module {
        testSourceDirs += file("src/integrationTest/java")
        testResourceDirs += file("src/integrationTest/resources")
        testSourceDirs += file("src/e2eTest/java")
        testResourceDirs += file("src/e2eTest/resources")
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
//    jcenter()
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

wrapper {
    gradleVersion = "7.3.2"
}

apply plugin: 'org.owasp.dependencycheck'

dependencyCheck {
    failBuildOnCVSS = 0
    suppressionFile = 'gradle/owasp-suppressions.xml'
    formats = [Format.XML, Format.HTML]
}

allprojects { project ->

    plugins.withType(JavaPlugin).whenPluginAdded {
        // do Java specific configurations
        project.apply plugin: 'io.spring.dependency-management'
        project.apply plugin: 'idea'
        project.apply plugin: 'java-test-fixtures'
        project.apply plugin: 'java-library'
        project.apply plugin: 'jacoco'

        jar.enabled = true

        group = 'io.northern-lights'
        version = '0.0.1-SNAPSHOT'
        sourceCompatibility = '17'

        repositories {
            mavenCentral()
            maven { url 'https://repo.spring.io/milestone' }
            maven { url 'https://repo.spring.io/snapshot' }
        }

        sourceSets {
            integrationTest {
                compileClasspath += sourceSets.main.output
                runtimeClasspath += sourceSets.main.output
            }
        }

        configurations {
            compileOnly {
                extendsFrom annotationProcessor
            }
            integrationTestImplementation.extendsFrom testImplementation
            integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
        }

        dependencyManagement {
            imports {
                mavenBom SpringBootPlugin.BOM_COORDINATES
//                mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${GAVerifierVersion}"
            }
            dependencies {
                dependency "net.logstash.logback:logstash-logback-encoder:7.0.1"
                dependency "ch.qos.logback.contrib:logback-json-classic:0.1.5"
                dependency "ch.qos.logback.contrib:logback-jackson:0.1.5"

                dependency "com.auth0:java-jwt:3.18.2"
                dependency "com.auth0:jwks-rsa:0.21.0"
                dependency "commons-io:commons-io:2.8.0"

                dependency "io.r2dbc:r2dbc-postgresql:0.8.12.RELEASE"
                dependency "org.postgresql:r2dbc-postgresql:0.9.1.RELEASE"
                // sql driver for liquibase migration during test
                dependency "org.postgresql:postgresql:42.3.3"

                dependency "io.debezium:debezium-embedded:1.9.5.Final"
                dependency "io.debezium:debezium-connector-postgres:1.9.5.Final"

                dependency "net.javacrumbs.json-unit:json-unit-assertj:2.27.0"
                dependency "org.testcontainers:postgresql:1.16.2"
                dependency "org.testcontainers:r2dbc:1.16.2"
                dependency "org.testcontainers:junit-jupiter:1.16.2"

                dependency "org.springdoc:springdoc-openapi-webflux-core:1.5.13"
                dependency "org.springdoc:springdoc-openapi-webflux-ui:1.5.13"
                dependency "org.springdoc:springdoc-openapi-security:1.5.13"
                dependency "org.springdoc:springdoc-openapi-data-rest:1.5.13"

//                // Temporary Jackson version increase for green JFrog
//                dependency "com.fasterxml.jackson.core:jackson-annotations:2.14.0"
//                dependency "com.fasterxml.jackson.core:jackson-databind:2.14.0"
//                dependency "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.0"
//                dependency "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.0"
//
//                // Temporary io.netty version increase for green JFrog
//                dependency "io.netty:netty-codec-http:4.1.71.Final"
//                dependency "io.netty:netty-codec-http2:4.1.71.Final"
//                dependency "io.netty:netty-handler-proxy:4.1.71.Final"
            }
        }

        dependencies {
            compileOnly "org.projectlombok:lombok"
            annotationProcessor "org.projectlombok:lombok"

            implementation "org.slf4j:slf4j-api"
            implementation "net.logstash.logback:logstash-logback-encoder"
            implementation "ch.qos.logback:logback-classic"
            implementation "ch.qos.logback.contrib:logback-json-classic"
            implementation "ch.qos.logback.contrib:logback-jackson"

            implementation "io.projectreactor:reactor-core"

            // https://docs.spring.io/spring-boot/docs/current/reference/html/boot-features-testing.html#boot-features-testing
            testImplementation("org.springframework.boot:spring-boot-starter-test") {
                exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
            }
            testImplementation "org.junit.jupiter:junit-jupiter-api"
            testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
            testImplementation "org.mockito:mockito-junit-jupiter"
            testImplementation "io.projectreactor:reactor-test"

            testCompileOnly "org.projectlombok:lombok"
            testAnnotationProcessor "org.projectlombok:lombok"
//            testImplementation "org.slf4j:slf4j-api"

            // no value, only to avoid a specific warning logs during build
            implementation 'com.google.code.findbugs:annotations:3.0.1u2'
        }

        compileJava.dependsOn(processResources)

        compileJava {
            options.compilerArgs << '-Xlint:unchecked'
        }

        compileTestJava {
            options.compilerArgs << '-Xlint:unchecked'
        }

        test {
//            minHeapSize = "128m"
//            maxHeapSize = "6096m"
            group = LifecycleBasePlugin.VERIFICATION_GROUP
            description = "Runs the unit tests."
            useJUnitPlatform()

            testLogging {
                showStandardStreams = true
                events = ["failed", "skipped"]
                exceptionFormat "full"
            }
        }

        task integrationTest(type: Test) {
//            maxHeapSize = "256m"
            group = LifecycleBasePlugin.VERIFICATION_GROUP
            description = 'Runs the integration tests.'
            useJUnitPlatform()

            testClassesDirs = sourceSets.integrationTest.output.classesDirs
            classpath = sourceSets.integrationTest.runtimeClasspath

            shouldRunAfter test
        }

        check.dependsOn test
        check.dependsOn integrationTest
        check.finalizedBy jacocoTestReport

        jacocoTestReport {
            executionData.setFrom fileTree('.').include("**/build/jacoco/*.exec")
            reports {
                xml.enabled true
                html.enabled true
                csv.enabled false
            }
        }
    }

    plugins.withType(JavaTestFixturesPlugin).whenPluginAdded {
        test {
            // Issue when using spring boot plugin with java-test-fixtures plugin
            // https://github.com/gradle/gradle/issues/11696
            // Make sure the classes dir is used on the test classpath.
            // When test fixtures are involved, the JAR is used by default
            classpath = sourceSets.main.output.classesDirs + classpath - files(jar.archiveFile)
        }
        dependencies {
            testFixturesCompileOnly "org.projectlombok:lombok"
            testFixturesAnnotationProcessor "org.projectlombok:lombok"
            testFixturesImplementation "org.slf4j:slf4j-api"

//            testFixturesImplementation "org.mockito:mockito-junit-jupiter"

            // no value, only to avoid a specific warning logs during build
            testFixturesImplementation 'com.google.code.findbugs:annotations:3.0.1u2'
        }
    }
//
//    plugins.withType(SpringCloudContractVerifierGradlePlugin).whenPluginAdded {
//        // do Spring Cloud Contract specific configurations
//        project.apply plugin: 'groovy'
//
//        dependencies {
//            testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
//
//            testImplementation "org.codehaus.groovy:groovy-all:${groovyVersion}"
//            // example with adding Spock core and Spock Spring
//            //    testImplementation "org.spockframework:spock-core:${spockVersion}"
//            //    testImplementation "org.spockframework:spock-spring:${spockVersion}"
//        }
//
//        sourceSets {
//            integrationTest.java.srcDirs += new File(project.buildDir, "generated-test-source").toString()
//        }
//
//        compileTestGroovy.enabled = false
//    }

    plugins.withType(SpringBootPlugin).whenPluginAdded {
        sourceSets {
            e2eTest {
                compileClasspath += sourceSets.main.output
                runtimeClasspath += sourceSets.main.output
            }
        }

        configurations {
            e2eTestImplementation.extendsFrom testImplementation
            e2eTestRuntimeOnly.extendsFrom testRuntimeOnly
            e2eTestCompileOnly.extendsFrom testCompileOnly
            e2eTestAnnotationProcessor.extendsFrom testAnnotationProcessor
            runtimeClasspath {
                extendsFrom developmentOnly
            }
        }

        // do Spring specific configurations
        dependencies {
            annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
            developmentOnly "org.springframework.boot:spring-boot-devtools"
            //
            //            // https://docs.spring.io/spring-boot/docs/current/reference/html/boot-features-testing.html#boot-features-testing
            //            testImplementation("org.springframework.boot:spring-boot-starter-test") {
            //                exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
            //            }
//            // Error with Spring Boot 2.4.0 and Spring Integration FTP (5.4.1)
//            // which loads commons-net:3.7 (broken version)
//            implementation "commons-net:commons-net:3.7.1"
        }
        compileJava.inputs.files(processResources)


//        processResources {
//            with copySpec {
//                from 'src/main/resources'
//                include '**/application*.yml'
//                include '**/application*.yaml'
//                include '**/application*.properties'
//                project.properties.findAll().each {
//                    prop ->
//                        if (prop.value != null) {
//                            filter(ReplaceTokens, tokens: [ (prop.key): prop.value])
//                            filter(ReplaceTokens, tokens: [ ('project.' + prop.key): prop.value])
//                        }
//                }
//            }
//        }

        task e2eTest(type: Test) {
            group = LifecycleBasePlugin.VERIFICATION_GROUP
            description = 'Runs the end-to-end tests.'
            useJUnitPlatform()

            testClassesDirs = sourceSets.e2eTest.output.classesDirs
            classpath = sourceSets.e2eTest.runtimeClasspath

            shouldRunAfter integrationTest
        }
        check.dependsOn e2eTest
    }
}
